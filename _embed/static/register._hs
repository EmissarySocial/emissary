-- Global actions for the whole page.
on htmx:beforeRequest
	add [@disabled=true] to #submitButton
	
	remove .invalid from <input/>
	tell <[role=alert]/>
		remove yourself
	end

	tell <[role=note]/>
		show yourself
	end
end

on RegistrationSuccess
	set lastPage to sessionStorage.getItem("signin-return")
	call sessionStorage.removeItem("signin-return")
	if lastPage is empty then
		set lastPage to "/home"
	end
	set window.location to lastPage
end

on eventValidatorError
	remove [@disabled] from #submitButton
end

-- validates for specific fields based on DOM events.
behavior eventValidator(name)

	on keyup
		remove .invalid from <input/> in me

	on eventValidatorError from window
				
		if event.detail[name] == null then 
			exit
		end

		tell <[role=note]/> in me
			hide yourself
		end

		tell <[role=alert]/> in me
			remove yourself
		end

		add .invalid to <input/> in me
		put `<div role="alert" class="text-sm red">${event.detail[name]}</div>` at the end of me

	end

end
