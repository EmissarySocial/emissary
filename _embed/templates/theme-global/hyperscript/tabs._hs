behavior TabContainer

	-- init handles the default tab selection.  If a document hash exists 
	-- (and points to one of our tabs) then select it first.  Otherwise,
	-- select the first tab in the list
	init
		set :myTablist to first <[role=tablist] /> in me
		set target to the first <[aria-selected="true"]/> in :myTablist
		
		if target is null then 
			set target to first <[role=tab]/> in :myTablist
		end

		send selectTab to target
	end

	-- select highlighted tab on ENTER (additional key to select tabs)
	-- on keydown[keyCode==13]
	--	send select to first <[role=tab]:focus/>
	--	halt the event

	-- handle mouse clicks directly on tabs
	on mousedown(target)[button==0] from <[role=tablist] [role=tab] />
		send selectTab to target
		halt the event
	end

	on click from <[role=tablist] [role=tab] />
		send selectTab to target
		halt the event

	-- handle touch events for phones and tablets
	on touchstart(target) from <[role=tablist] [role=tab] />
		send selectTab to target
		halt the event
	end

	on selectTab(target)

		set target to the closest <[role=tab] /> to the target

		if :myTablist is not the closest <[role=tablist] /> to the target then
			exit
		end

		if target [@disabled] is not null then 
			exit
		end

		for tab in <[role=tab] /> in me
			if :myTablist is the closest <[role=tablist] /> to tab
				if tab == target
					add [@aria-selected="true"] to tab
				else
					remove [@aria-selected] from tab
				end
			end
		end

		for panel in <[role=tabpanel] />
			if panel.parentNode is me then
				set {hidden: (panel[@id] != target[@aria-controls])} on panel
			end
		end
	end
end

