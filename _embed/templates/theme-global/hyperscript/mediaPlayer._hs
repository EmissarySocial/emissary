behavior MediaPlayer

	init
		set :trackNumber to 0
		set :playButton to the first <.media-Play /> in me
		set :pauseButton to the first <.media-Pause /> in me
		set :nextButton to the first <.media-Next /> in me
		set :prevButton to the first <.media-Prev /> in me
		set :loopButton to the first <.media-Loop /> in me
		set :progressBar to the first <.media-Progress /> in me
		set :media to the first <audio,video /> in me

		tell <.media-show-when-playing /> in me
			hide yourself
		end

		tell <.media-show-when-paused /> in me
			show yourself
		end

	end

	on click from <.media-Play /> in me
		trigger Play
	end

	on click from <.media-Pause /> in me
		trigger Pause
	end

	on click from <.media-Prev /> in me
		trigger Prev
	end

	on click from <.media-Next /> in me
		trigger Next
	end

	on DragStart from <.media-Progress /> in me
		pause() the :media
	end

	on DragEnd from <.media-Progress /> in me
		set percent to the :progressBar's @value
		trigger Seek(percent:percent)
		play() the :media
	end

	on timeupdate from <audio /> in me or
		timeupdate from <video /> in me

		if :media.duration is undefined then
			exit
		end

		if :progressBar is undefined then
			exit
		end

		set percent to (:media.currentTime / :media.duration) * 100
		send MoveHandle(percent:percent) to :progressBar
	end

	on ended from <audio /> in me or
		ended from <video /> in me

		set nextTrackNumber to :trackNumber + 1
		if nextTrackNumber < :tracks.length then
			trigger Play(trackNumber:nextTrackNumber)
			exit
		end

		set :trackNumber to 0
		trigger Pause
	end

	on Play(trackNumber)

		set tracks to <[data-track-url] /> in me

		if trackNumber is empty then 
			set trackNumber to :trackNumber
		end

		if trackNumber < 0 then 

			if :loopButton is undefined then
				trigger Pause
				exit
			end

			if not :loopButton.classlist.contains("selected") then
				trigger Pause
				exit
			end

			set trackNumber to tracks.length - 1

		else if trackNumber >= tracks.length then

			if :loopButton is undefined then
				trigger Pause
				exit
			end

			if not :loopButton.classlist.contains("selected") then
				trigger Pause
				exit
			end

			set trackNumber to 0
		end

		set :trackNumber to trackNumber
		set trackElement to tracks[trackNumber]
		set url to the trackElement's @data-track-url

		if url is empty then
			exit
		end

		if url is not equal to :media's @src then
			set :media.src to url
		end

		play() the :media

		take .playing from tracks for trackElement

		tell <.media-show-when-playing /> in me
			show yourself
		end

		tell <.media-show-when-paused /> in me
			hide yourself
		end

	end

	on Seek(percent)
		if :media.duration is undefined then
			exit
		end

		set :media.currentTime to (:media.duration * (percent / 100))
	end

	on Pause

		pause() the :media

		for trackElement in <[data-track-url] /> in me
			remove .playing from trackElement
		end

		tell <.media-show-when-playing /> in me
			hide yourself
		end

		tell <.media-show-when-paused /> in me
			show yourself
		end

	end

	on Prev
		set nextTrackNumber to :trackNumber - 1
		trigger Play(trackNumber:nextTrackNumber)
	end

	on Next
		set nextTrackNumber to :trackNumber + 1
		trigger Play(trackNumber:nextTrackNumber)
	end

	on SetProgress(percent) from <audio /> in me
		
		if :media.duration is not undefined then
			set :media.currentTime to (:media.duration * (percent / 100))
		end
	end

end
