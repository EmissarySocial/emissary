behavior Modal

	init
		add [@role="dialog"]
		set title to the first <h1,h2,h3/> in me

		if (title is not empty) then
			
			if title.id is empty  then 
				set title.id to "modal-title" 
			end

			set the @aria-labelledby to the title's id
		end

		-- Prevent window from scrolling underneath the modal
		-- From: https://stackoverflow.com/questions/44103023/stop-scrolling-on-body-when-modal-is-open-and-allow-scroll-on-modal-divs
		set document.body.style.overflow to "hidden"

		wait a tick then
		add .ready to #modal

		log #modal-body
		if #modal-body is not null then
			set firstElement to the first <[tabindex]/> in the #modal-body
		else
			set firstElement to the first <[tabindex]/> in me
		end

		if firstElement is not null then 
			focus() the firstElement
		end

		send modalReady
	end

	on closeModal from window	

		if #modal is empty then 
			exit
		end

		-- animate the modal closing
		remove .ready from #modal
		settle

		-- done
		remove #modal

		-- reset window scrolling
		-- From: https://stackoverflow.com/questions/44103023/stop-scrolling-on-body-when-modal-is-open-and-allow-scroll-on-modal-divs
		set document.body.style.overflow to ""
	end
	
	on click (target)
		if the target's id is "modal-underlay" then
			trigger closeModal
		end
	end

	on keydown[key=="Escape"] from window
		if #modal is not empty then 
			trigger closeModal
			halt the event
		end
	end

	on keydown[key=="Tab"]
		set focusedElement to the document's activeElement

		if event.shiftKey then

			if focusedElement is the first <[tabindex]/> in me then
				focus() the last <[tabindex]/> in me
				halt the event
			end
			
		else if focusedElement is the last <[tabindex]/> in me then
			focus() the first <[tabindex]/> in me
			halt the event
		end
		
	end

	on htmx:beforeSwap from window
		if event.detail.target is the first <aside/> then
			make a RegExp from "^<div " called firstDiv
			set event.detail.serverResponse to event.detail.serverResponse.replace(firstDiv, "<div class='ready' ")
		end
	end
end